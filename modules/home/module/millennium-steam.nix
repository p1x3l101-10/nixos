{ config, lib, ... }:

let
  inherit (lib) mkEnableOption mkOption mkIf types;
  cfg = config.programs.millennium-steam;
in {
  imports = (lib.internal.confTemplates.importList ./support/millennium-steam/themes);
  options.programs.millennium-steam = {
    enable = mkEnableOption "millenium steam user config";
    quickCss = mkOption {
      description = "CSS changes to make without theming";
      type = types.lines;
      default = "";
    };
    settings = let
      mkQO = default: type: mkOption {
        description = "Millenium JSON config value";
        inherit default type;
      };
      mkSO = default: mkQO default types.str;
      mkBO = default: mkQO default types.bool;
      mkIO = default: mkQO default types.int;
    in {
      general = {
        accentColor = mkSO "DEFAULT_ACCENT_COLOR";
        checkForMillenniumUpdates = mkBO true;
        checkForPluginAndThemeUpdates = mkBO true;
        injectCSS = mkBO true;
        injectJavascript = mkBO true;
        milleniumUpdateChannel = mkSO "stable";
        onMilleniumUpdate = mkIO 1;
        shouldShowThemePluginUpdateNotifications = mkBO true;
      };
      misc = {
        hasShownWelcomeModal = mkBO false;
      };
      themes = {
        activeTheme = mkSO "default";
        allowedScripts = mkBO true;
        allowedStyles = mkBO true;
        conditions = mkOption {
          description = "Theme conditions";
          type = with types; attrsOf (attrsOf str);
          default = {};
        };
        themeColors = mkOption {
          description = "Theme colors (in css format)";
          type = with types; attrsOf (attrsOf str);
          default = {};
        };
      };
    };
  };
  config = mkIf (cfg.enable) {
    xdg.configFile = {
      "millennium/quickcss.css".text = ''
        /* Generated by Home Manager, DO NOT EDIT THIS FILE DIRECTLY! */
      '' + cfg.quickCss;
      "millennium/config.json".text = builtins.toJSON cfg.settings;
    };
  };
}
